class_name JestSDKSingleton
extends Node

## Emitted when the SDK initialization completes. Check success parameter.
signal sdk_initialized(success: bool)

## Emitted when player data is changed locally via set_value/set_int/etc.
signal player_data_changed(key: String, value: String)

## Whether the SDK has been initialized successfully.
var is_initialized: bool:
	get: return _bridge.is_initialized

## Whether the SDK is running in a web browser (true) or mock/editor mode (false).
var is_web: bool:
	get: return _bridge._is_web

## Provides access to player-related functionality and data.
var player: JestPlayer

## Provides access to social/profile helpers.
var social: JestSocial

## Provides access to purchase related functionality and data.
var payment: JestPayment

## Provides access to notification management functionality.
var notifications: JestNotifications

## Provides access to referral functionality and statistics.
var referrals: JestReferrals

## Provides access to navigation functionality for redirecting between games.
var navigation: JestNavigation

## Provides access to internal/experimental SDK functionality.
## These methods may change without notice.
var internal_api: JestInternal

## Provides access to the platform registration overlay flow.
var registration_overlay: JestRegistrationOverlay

## Access to mock configuration. Only available in non-web mode (returns null otherwise).
var mock: JestBridgeMock:
	get: return _bridge._mock if not _bridge._is_web else null

var _bridge: JestBridge


func _ready() -> void:
	_bridge = JestBridge.new()
	_bridge.setup(get_tree())
	player = JestPlayer.new(_bridge, func(k: String, v: String):
		player_data_changed.emit(k, v)
	)
	payment = JestPayment.new(_bridge)
	social = JestSocial.new(player)
	notifications = JestNotifications.new(_bridge)
	referrals = JestReferrals.new(_bridge)
	navigation = JestNavigation.new(_bridge)
	internal_api = JestInternal.new(_bridge)
	registration_overlay = JestRegistrationOverlay.new(_bridge)


## Initializes the Jest SDK and ensures it's ready for use.
## Returns true on success, false on failure.
## options Dictionary keys:
##   auto_login_reminders: bool (optional, default true) — when false, disables
##     automatic login reminder popups for guest users.
##     Manual login via login() is unaffected.
## Must be awaited: var success = await JestSDK.init_sdk()
func init_sdk(options: Dictionary = {}) -> bool:
	var init_options := {}
	if options.has("auto_login_reminders"):
		init_options["autoLoginReminders"] = options["auto_login_reminders"]
	var success := await _bridge.init_sdk(init_options)
	sdk_initialized.emit(success)
	return success


## Retrieves the entry payload data associated with the current session entry.
func get_entry_payload() -> Dictionary:
	return JestUtils.parse_json_dict(_bridge.get_entry_payload())


## Triggers the platform login flow and awaits the player dismissing the login
## popup. Resolves immediately if the player is already registered. The player's
## registered state is refreshed once login completes; use player.is_registered
## to check status.
## Should be awaited: await JestSDK.login()
func login(payload: Dictionary = {}) -> void:
	if player.is_registered:
		# Already registered — nothing to log into; resolve immediately.
		return
	var payload_string := ""
	if not payload.is_empty():
		payload_string = JSON.stringify(payload)
	await _bridge.login(payload_string)
	player.invalidate_cache()


## Shows the platform registration overlay and returns actions that game UI can
## wire to login and close buttons.
func show_registration_overlay(options: JestRegistrationOverlayOptions = null) -> JestRegistrationOverlayHandle:
	return registration_overlay.show(options)


## Opens the privacy policy page.
## @internal — not part of the supported public API.
func open_privacy_policy() -> void:
	_bridge.open_legal_page("privacy")


## Opens the terms of service page.
## @internal — not part of the supported public API.
func open_terms_of_service() -> void:
	_bridge.open_legal_page("terms")


## Opens the copyright page.
## @internal — not part of the supported public API.
func open_copyright() -> void:
	_bridge.open_legal_page("copyright")


## Triggers debug registration flow (debug builds only).
func debug_register() -> void:
	_bridge.debug_register()


## Gets the value of a feature flag by key.
## Returns JestFeatureFlagResult with typed accessors for string/bool/int/float.
## Must be awaited: var flag = await JestSDK.get_feature_flag("my_flag")
func get_feature_flag(key: String) -> JestFeatureFlagResult:
	if key.strip_edges().is_empty():
		return JestFeatureFlagResult.make_error("key cannot be empty")
	var cb_result: Dictionary = await _bridge.get_feature_flag(key)
	if cb_result["timed_out"]:
		return JestFeatureFlagResult.make_error("timeout")
	if not cb_result["error"].is_empty():
		return JestFeatureFlagResult.make_error(cb_result["error"])
	var raw: String = cb_result["result"]
	var parsed = JSON.parse_string(raw)
	return JestFeatureFlagResult.make_success(parsed if parsed != null else raw)


## Validates a player name against platform rules.
## Must be awaited: var result = await JestSDK.validate_name("player1")
func validate_name(name_value: String) -> JestNameValidationResult:
	if name_value.strip_edges().is_empty():
		return JestNameValidationResult.make_error("name cannot be empty")
	var cb_result: Dictionary = await _bridge.validate_name(name_value)
	if cb_result["timed_out"]:
		return JestNameValidationResult.make_error("timeout")
	if not cb_result["error"].is_empty():
		return JestNameValidationResult.make_error(cb_result["error"])
	return JestNameValidationResult.from_dict(JestUtils.parse_json_dict(cb_result["result"]))


## Records a custom analytics event for the current player.
## Events appear in the Developer Console for tracking milestones, funnels, and feature usage.
## event_name: stable, lowercase, snake_case name (e.g. "level_complete").
## properties: optional structured data attached to the event.
func capture_event(event_name: String, properties: Dictionary = {}) -> void:
	if event_name.strip_edges().is_empty():
		push_error("[JestSDK] event_name cannot be empty")
		return
	var props_json := JSON.stringify(properties) if not properties.is_empty() else ""
	_bridge.capture_event(event_name, props_json)


## Reports loading progress to the platform loading screen overlay.
## Only works when the game's loading screen mode is set to "manual".
## progress: Loading progress from 0 to 100. Setting to 100 dismisses the overlay.
func set_loading_progress(progress: float) -> void:
	_bridge.set_loading_progress(progress)


## Signals that the game is ready to be played — all assets have loaded and the
## player can interact. Also dismisses the manual loading overlay unless progress
## 100 was already sent. Safe to call at any time; calls after the first are no-ops.
func mark_game_loaded() -> void:
	_bridge.mark_game_loaded()


## Returns the current player's profile (username and sized avatar URL).
## Mirrors the HTML5 SDK's [code]social.getProfile({ avatarSize })[/code].
## Supported sizes: 64, 128, 256, 512, 1000 (default). Other values are
## bucketed down to the next supported size.
func get_profile(avatar_size: int = 1000) -> JestPlayerProfile:
	return social.get_profile(avatar_size)


## Returns a CDN URL for a bot avatar, deterministically seeded by [param username].
## Use the smallest [param size] that fits your UI. Supported sizes: 64, 128, 256,
## 512, 1000 (default). Other values are bucketed down to the next supported size.
func get_bot_avatar(username: String, size: int = 1000) -> String:
	return social.get_bot_avatar(username, size)


## [b]Deprecated.[/b] Use [code]get_profile(size).avatar_url[/code] instead.
## Returns a CDN URL for the current player's avatar at the requested [param size],
## routed through Cloudflare Image Resizing so Godot can decode it reliably.
## Returns an empty string when the player has no avatar.
## Supported sizes: 64, 128, 256, 512, 1000 (default). Intermediate values bucket
## down to the next supported size.
func get_player_avatar(size: int = 1000) -> String:
	return social.get_player_avatar(size)
